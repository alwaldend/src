load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//tools/docs/main/bzl:al_docs_files.bzl", "al_docs_files")
load("//tools/http_server/main/bzl:al_http_server_binary.bzl", "al_http_server_binary")
load("//tools/hugo/main/bzl:al_hugo_binary.bzl", "al_hugo_binary")
load("//tools/hugo/main/bzl:al_hugo_run_binary.bzl", "al_hugo_run_binary")
load("//tools/hugo/main/bzl:al_hugo_site.bzl", "al_hugo_site")
load("//tools/toml/main/bzl:al_toml_data.bzl", "al_toml_data")

build_test(
    name = "site_test",
    targets = [
        ":site",
    ],
)

_BUILD_FLAGS = [
    "build",
    "--logLevel",
    "debug",
    "--panicOnWarning",
    "--buildDrafts",
    "--buildExpired",
    "--buildFuture",
    "--printI18nWarnings",
    "--printPathWarnings",
    # "--printUnusedTemplates", /404.html is unused
    "--destination",
    "{}/k8-fastbuild/bin/{}/{}".format(
        "/".join([".."] * 6),
        package_name(),
        "site.destination",
    ),
]

al_docs_files(
    name = "docs",
    srcs = glob(["*.md"]),
    prefix = package_name(),
    visibility = ["//projects:__pkg__"],
    deps = ["//projects/alwaldend.com/releases:docs"],
)

al_hugo_site(
    name = "site_site",
    postcss = "//tools/postcss",
    site = ":site_source_archive",
)

al_hugo_binary(
    name = "site_hugo",
    site = ":site_site",
)

al_hugo_run_binary(
    name = "site",
    arguments = select({
        "//tools/bazel_configs:release": _BUILD_FLAGS + [
            "--baseURL",
            "https://alwaldend.com",
        ],
        "//conditions:default": _BUILD_FLAGS,
    }),
    hugo = ":site_hugo",
    out_dirs = ["site.destination"],
)

pkg_tar(
    name = "site_archive",
    srcs = [":site"],
)

pkg_tar(
    name = "site_source_archive",
    srcs = [
        ":site_config",
        "//:docs",
        "//projects/alwaldend.com/assets",
        "//projects/alwaldend.com/content",
        "//projects/alwaldend.com/data",
        "//projects/alwaldend.com/i18n",
        "//projects/alwaldend.com/layouts",
        "@com_alwaldend_src_hugo",
    ],
    strip_prefix = ".",
)

genrule(
    name = "deploy",
    srcs = [":site_archive"],
    outs = ["deploy.sh"],
    cmd = """
cat - >$(@) <<EOF
#!/usr/bin/env sh
set -eu

archive="\\$${PWD}/$(execpath :site_archive)"
temp=\\$$(mktemp -d)
trap "rm -rf \\$${temp}" EXIT
cd "\\$${temp}"
git clone \
    --depth 1 \
    --single-branch \
    --branch master \
    git@github.com:alwaldend/alwaldend.github.io.git
cd alwaldend.github.io
git checkout -B pages
tar -xf "\\$${archive}" --strip-components 1
git add .
git commit -m "Update the site"
git push -f
EOF
    """,
    executable = True,
)

pkg_filegroup(
    name = "site_filegroup",
    srcs = [":site_files"],
)

pkg_files(
    name = "site_files",
    srcs = [":site"],
    renames = {":site": "src"},
)

al_http_server_binary(
    name = "site_serve",
    srcs = [":site_filegroup"],
    arguments = [
        "--bind",
        "127.0.0.1",
        "--directory",
        "src",
        "1313",
    ],
)

pkg_files(
    name = "site_config",
    srcs = [":site_config_src"],
    renames = {"hugo.toml": "config/_default/hugo.toml"},
)

al_toml_data(
    name = "site_config_src",
    srcs = ["hugo.toml"],
)
