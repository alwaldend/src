load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//tools/docs:al_docs_files.bzl", "al_docs_files")
load("//tools/http_server:al_http_server_binary.bzl", "al_http_server_binary")
load("//tools/hugo:al_hugo_binary.bzl", "al_hugo_binary")
load("//tools/hugo:al_hugo_run_binary.bzl", "al_hugo_run_binary")
load("//tools/hugo:al_hugo_site.bzl", "al_hugo_site")
load("//tools/toml:al_toml_data.bzl", "al_toml_data")

_BUILD_FLAGS = [
    "build",
    "--logLevel",
    "debug",
    "--panicOnWarning",
    "--buildDrafts",
    "--buildExpired",
    "--buildFuture",
    "--printI18nWarnings",
    "--printPathWarnings",
    # "--printUnusedTemplates", /404.html is unused
    "--destination",
    "{}/k8-fastbuild/bin/{}/{}".format(
        "/".join([".."] * 6),
        package_name(),
        "docs.destination",
    ),
]

al_docs_files(
    name = "docs",
    srcs = glob(["*.md"]),
    prefix = package_name(),
    visibility = ["//projects:__pkg__"],
)

al_hugo_site(
    name = "site_site",
    postcss = "//tools/postcss",
    site = ":site_source_archive",
)

al_hugo_binary(
    name = "site_hugo",
    site = ":site_site",
)

al_hugo_run_binary(
    name = "site",
    arguments = select({
        "//tools/bzl/configs:ci": _BUILD_FLAGS + [
            "--baseURL",
            "https://alwaldend.github.io/src",
        ],
        "//conditions:default": _BUILD_FLAGS,
    }),
    hugo = ":site_hugo",
    out_dirs = ["docs.destination"],
)

pkg_filegroup(
    name = "site_filegroup",
    srcs = [":site_files"],
)

pkg_files(
    name = "site_files",
    srcs = [":site"],
    renames = {":site": "src"},
)

al_http_server_binary(
    name = "site_serve",
    srcs = [":site_filegroup"],
    arguments = [
        "--bind",
        "127.0.0.1",
        "--directory",
        "src",
        "1313",
    ],
)

pkg_tar(
    name = "site_archive",
    srcs = [":docs"],
    visibility = ["//:__subpackages__"],
)

pkg_tar(
    name = "site_source_archive",
    srcs = [
        ":site_config",
        ":site_data",
        "//projects/alwaldend.com/content:docs",
        "//projects/alwaldend.com/i18n",
        "//projects/alwaldend.com/layouts",
        "//tools/hugo/themes",
    ],
    strip_prefix = ".",
)

pkg_filegroup(
    name = "site_data",
    srcs = [":site_data_files"],
    prefix = "data/{}".format(package_name()),
)

pkg_files(
    name = "site_data_files",
    srcs = [
        "//data/misc:authors",
        "//data/misc:books",
    ],
)

pkg_files(
    name = "site_config",
    srcs = [":site_config_src"],
    renames = {"hugo.toml": "config/_default/hugo.toml"},
)

al_toml_data(
    name = "site_config_src",
    srcs = ["hugo.toml"],
)
