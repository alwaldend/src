load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@rules_go//go:def.bzl", "go_binary", "go_cross_binary", "go_library")
load("@rules_go//go/platform:list.bzl", "GOOS_GOARCH")
load("@rules_oci//oci:defs.bzl", "oci_image")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

_GOOS_GOARCH = [
    (goos, goarch)
    for goos, goarch in GOOS_GOARCH
    if goos not in ("qnx", "osx", "darwin", "ios", "android", "nacl")
]

build_test(
    name = "build_test",
    targets = [
        ":go_crossbinary",
        ":go_tar",
        ":go",
        ":go_oci",
    ],
)

go_library(
    name = "go_lib",
    srcs = [
        "cmd.go",
        "downloader.go",
        "generator.go",
        "main.go",
        "repo.go",
    ],
    importpath = "git.alwaldend.com/alwaldend/src/projects/leetcode_downloader/main/go",
    visibility = ["//visibility:private"],
    deps = [
        "//projects/leetcode_downloader/main/go/model",
        "//projects/leetcode_downloader/main/proto/contracts",
        "@com_github_burntsushi_toml//:toml",
    ],
)

go_binary(
    name = "go",
    embed = [":go_lib"],
    visibility = ["//:__subpackages__"],
)

pkg_tar(
    name = "go_tar",
    srcs = [":go"],
    visibility = ["//:__subpackages__"],
)

oci_image(
    name = "go_oci",
    base = "@com_alwaldend_src_oci_alpine",
    tars = [":go_tar"],
    visibility = ["//:__subpackages__"],
)

[
    go_cross_binary(
        name = "go_cross_binary.{}_{}.exe".format(goos, goarch),
        platform = "@rules_go//go/toolchain:{}_{}".format(goos, goarch),
        target = ":go",
    )
    for goos, goarch in _GOOS_GOARCH
]

filegroup(
    name = "go_crossbinary",
    srcs = ["go_cross_binary.{}_{}.exe".format(goos, goarch) for goos, goarch in _GOOS_GOARCH],
    visibility = ["//projects/leetcode_downloader/releases:__pkg__"],
)
