load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files", "pkg_mkdirs", "strip_prefix")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//tools/docs:al_docs_files.bzl", "al_docs_files")
load("//tools/make_install:al_make_install_filegroup.bzl", "al_make_install_filegroup")
load("//tools/sh:al_sh_library.bzl", "al_sh_library")

_SCRIPTS = [script.removesuffix(".sh") for script in glob(["*.sh"])]

al_make_install_filegroup(
    name = "make_install_filegroup",
    srcs = [":filegroup"],
    pkg_prefix = "bin",
    visibility = ["//projects/dotfiles:__pkg__"],
)

al_docs_files(
    name = "docs",
    srcs = glob(["*.md"]),
    prefix = package_name(),
    visibility = ["//projects/dotfiles:__pkg__"],
)

pkg_filegroup(
    name = "filegroup",
    srcs = [":files"],
    prefix = ".local/bin",
)

[
    al_sh_library(
        name = script,
        srcs = ["{}.sh".format(script)],
    )
    for script in _SCRIPTS
]

pkg_files(
    name = "files",
    srcs = _SCRIPTS,
    renames = {
        "{}.sh".format(script): script
        for script in _SCRIPTS
    },
)
