load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//tools/bzl:al_bzl_target_doc.bzl", "al_bzl_target_doc")
load("//tools/git:al_git_changelog.bzl", "al_git_changelog")
load("//tools/http_server:al_http_server_binary.bzl", "al_http_server_binary")
load("//tools/hugo:al_hugo_binary.bzl", "al_hugo_binary")
load("//tools/hugo:al_hugo_run_binary.bzl", "al_hugo_run_binary")
load("//tools/hugo:al_hugo_site.bzl", "al_hugo_site")
load("//tools/toml:al_toml_data.bzl", "al_toml_data")

al_git_changelog(
    name = "changelog",
    visibility = ["//:__subpackages__"],
)

al_hugo_site(
    name = "docs_site",
    postcss = "//tools/postcss",
    site = ":docs_source_archive",
)

al_hugo_binary(
    name = "docs_hugo",
    site = ":docs_site",
)

_BUILD_FLAGS = [
    "build",
    "--logLevel",
    "debug",
    "--panicOnWarning",
    "--buildDrafts",
    "--buildExpired",
    "--buildFuture",
    "--printI18nWarnings",
    "--printPathWarnings",
    # "--printUnusedTemplates", /404.html is unused
    "--destination",
    "{}/k8-fastbuild/bin/{}/{}".format(
        "/".join([".."] * 6),
        package_name(),
        "docs.destination",
    ),
]

al_hugo_run_binary(
    name = "docs",
    arguments = select({
        "//tools/bzl/configs:ci": _BUILD_FLAGS + [
            "--baseURL",
            "https://alwaldend.github.io/src",
        ],
        "//conditions:default": _BUILD_FLAGS,
    }),
    hugo = ":docs_hugo",
    out_dirs = ["docs.destination"],
)

pkg_filegroup(
    name = "docs_filegroup",
    srcs = [":docs_files"],
)

pkg_files(
    name = "docs_files",
    srcs = [":docs"],
    renames = {":docs": "src"},
)

al_http_server_binary(
    name = "docs_serve",
    srcs = [":docs_filegroup"],
    arguments = [
        "--bind",
        "127.0.0.1",
        "1313",
    ],
)

pkg_tar(
    name = "docs_archive",
    srcs = [":docs"],
    visibility = ["//:__subpackages__"],
)

pkg_tar(
    name = "docs_source_archive",
    srcs = [
        ":docs_config",
        ":docs_data",
        "//docs/alwaldend.com/content",
        "//docs/alwaldend.com/i18n",
        "//docs/alwaldend.com/layouts",
        "//tools/hugo/themes",
    ],
    strip_prefix = ".",
)

pkg_filegroup(
    name = "docs_data",
    srcs = [":docs_data_files"],
    prefix = "data/{}".format(package_name()),
)

pkg_files(
    name = "docs_data_files",
    srcs = [
        "//data/misc:authors",
        "//data/misc:books",
    ],
)

pkg_files(
    name = "docs_config",
    srcs = [":docs_config_src"],
    renames = {"hugo.toml": "config/_default/hugo.toml"},
)

al_toml_data(
    name = "docs_config_src",
    srcs = ["hugo.toml"],
)

al_bzl_target_doc(
    name = "bazel_target_doc",
    visibility = ["//:__subpackages__"],
)
