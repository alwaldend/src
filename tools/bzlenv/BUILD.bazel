load("@rules_shell//shell:sh_binary.bzl", "sh_binary")
load("//tools/ansible/main/bzl:al_ansible_scripts.bzl", "AL_ANSIBLE_SCRIPTS")
load("//tools/bzl:al_bzl_library_map.bzl", "al_bzl_library_map")
load("//tools/bzlenv:al_bzlenv_binary.bzl", "al_bzlenv_binary")
load("//tools/docs:al_docs_files.bzl", "al_docs_files")
load("//tools/sh:al_sh_library.bzl", "al_sh_library")

_ROOT_TOOLS = [
    "taplo",
    "buf",
    "shellcheck",
    "cloc",
    "shfmt",
    "helm",
    "install_file",
    "template_files",
    "replace_section",
    "isort",
    "mypy",
    "hugo",
    "twine",
    "file_installer",
    "pnpm",
    "readme_tree",
    "prettier",
    "postcss",
    "shell_worker",
    "black",
    "flake8",
    "git_filter_repo",
    "tomlv",
    "stylua",
    "dnscontrol",
    "buildifier",
    "release",
]

al_bzl_library_map(
    name = "libs",
    libs = {
        "al_bzlenv_binary": [],
    },
    visibility = ["//:__subpackages__"],
)

al_docs_files(
    name = "docs",
    srcs = glob(["*.md"]),
    prefix = package_name(),
    visibility = ["//tools:__pkg__"],
    deps = [":libs.stardoc"],
)

al_sh_library(
    name = "bzlenv_activate",
    srcs = ["activate.sh"],
    visibility = ["//:__subpackages__"],
)

al_bzlenv_binary(
    name = "bzlenv",
    tools = {
        "gopackagesdriver": "//tools/sh:gopackagesdriver",
        "bazel": "//tools/sh:bazel",
    } | {
        tool: "//tools/{}".format(tool)
        for tool in _ROOT_TOOLS
    } | {
        script.replace("_", "-"): "//tools/ansible:{}".format(script)
        for script in AL_ANSIBLE_SCRIPTS
    },
)
