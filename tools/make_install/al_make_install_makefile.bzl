load("@bazel_skylib//lib:shell.bzl", "shell")
load("@rules_pkg//pkg:providers.bzl", "PackageFilegroupInfo", "PackageFilesInfo")
load("//tools/make_install:al_make_install_info.bzl", "AlMakeInstallInfo")

def _impl(ctx):
    makefile = ctx.actions.declare_file("{}.Makefile".format(ctx.label.name))
    package_files_info = PackageFilesInfo(
        attributes = {},
        dest_src_map = {"Makefile": makefile},
    )
    pkg_files = [(package_files_info, ctx.label)]
    pkg_dirs = []
    pkg_symlinks = []
    content = []

    content.append(".PHONY: install")
    content.append("install:")
    content.append("")
    content.append(".PHONY: diff")
    content.append("diff:")

    for filegroup in [
        filegroup
        for dep in ctx.attr.srcs
        for filegroup in dep[AlMakeInstallInfo].filegroups.to_list()
    ]:
        filegroup_pkg_files = filegroup.pkg_files.to_list()
        pkg_files.extend(filegroup_pkg_files)
        pkg_dirs.extend(filegroup.pkg_dirs.to_list())
        pkg_symlinks.extend(filegroup.pkg_symlinks.to_list())
        content.append("")
        content.append("## Generated by {}".format(filegroup.origin))
        content.append("")
        for filegroup_pkg_files, _ in filegroup_pkg_files:
            for path in filegroup_pkg_files.dest_src_map.keys():
                name = path.replace("/", "_").replace("-", "_")
                install_name = "install_{}".format(name)
                diff_name = "diff_{}".format(name)
                target_path = "{}/{}".format(filegroup.install_dir, path)
                content.append("")
                content.append(".PHONY: {}".format(install_name))
                content.append("install: {}".format(install_name))
                content.append("{}:".format(install_name))
                content.append('\t install {} "{}" "{}"'.format(
                    " ".join([shell.quote(arg) for arg in filegroup.install_args]),
                    path,
                    target_path,
                ))
                content.append("")
                content.append(".PHONY: {}".format(diff_name))
                content.append("diff: {}".format(diff_name))
                content.append("{}:".format(diff_name))
                content.append('\t diff {} "{}" "{}" || true'.format(
                    " ".join([shell.quote(arg) for arg in filegroup.diff_args]),
                    path,
                    target_path,
                ))
    content.append("")

    ctx.actions.write(
        output = makefile,
        content = "\n".join(content),
    )
    return [
        DefaultInfo(
            files = depset([makefile] + ctx.files.srcs),
        ),
        PackageFilegroupInfo(
            pkg_files = pkg_files,
            pkg_dirs = pkg_dirs,
            pkg_symlinks = pkg_symlinks,
        ),
    ]

al_make_install_makefile = rule(
    doc = "Create makefile for a make install filegroup",
    implementation = _impl,
    provides = [PackageFilegroupInfo],
    attrs = {
        "srcs": attr.label_list(
            providers = [AlMakeInstallInfo],
            doc = "Make install filegroups",
        ),
    },
)
