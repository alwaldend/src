load("@bazel_skylib//lib:shell.bzl", "shell")
load("@rules_pkg//pkg:providers.bzl", "PackageFilegroupInfo", "PackageFilesInfo")
load("//tools/make_install:al_make_install_filegroup_info.bzl", "AlMakeInstallFilegroupInfo")
load("//tools/make_install:al_make_install_info.bzl", "AlMakeInstallInfo")

def _impl(ctx):
    makefile = ctx.actions.declare_file("{}.Makefile".format(ctx.label.name))
    pkg_files = []
    pkg_dirs = []
    pkg_symlinks = []
    for src in ctx.attr.srcs:
        info = src[PackageFilegroupInfo]
        pkg_files.extend(info.pkg_files)
        pkg_dirs.extend(info.pkg_dirs)
        pkg_symlinks.extend(info.pkg_symlinks)
    pkg_files.append(
        (
            PackageFilesInfo(
                attributes = {},
                dest_src_map = {"Makefile": makefile},
            ),
            ctx.label,
        ),
    )

    filegroups = []
    filegroup_info = AlMakeInstallFilegroupInfo(
        install_dir = ctx.attr.install_dir,
        origin = ctx.label,
        install_args = ctx.attr.install_args,
        diff_args = ctx.attr.diff_args,
        filegroup = PackageFilegroupInfo(
            pkg_files = [] + pkg_files,
            pkg_dirs = [] + pkg_dirs,
            pkg_symlinks = [] + pkg_symlinks,
        ),
    )
    filegroups.append(filegroup_info)

    for dep in ctx.attr.deps:
        for info in dep[AlMakeInstallInfo].filegroups:
            pkg_dirs.extend(info.filegroup.pkg_dirs)
            pkg_symlinks.extend(info.filegroup.pkg_symlinks)
            for info_pkg_files, origin in info.filegroup.pkg_files:
                if "Makefile" in info_pkg_files.dest_src_map:
                    info_pkg_files.dest_src_map.pop("Makefile")
                pkg_files.append((info_pkg_files, origin))
            filegroups.append(info)

    content = []
    content.append(".PHONY: install")
    content.append("install:")
    content.append("")
    content.append(".PHONY: diff")
    content.append("diff:")
    for filegroup in filegroups:
        content.append("")
        content.append("## Generated by {}".format(filegroup.origin))
        content.append("")
        for pkg_files, _ in filegroup.filegroup.pkg_files:
            for path in pkg_files.dest_src_map.keys():
                if path == "Makefile":
                    continue
                name = path.replace("/", "_")
                install_name = "install_{}".format(name)
                diff_name = "diff_{}".format(name)
                target_path = "{}/{}".format(filegroup.install_dir, path)
                content.append("")
                content.append(".PHONY: {}".format(install_name))
                content.append("install: {}".format(install_name))
                content.append("{}:".format(install_name))
                content.append('\t install {} "{}" "{}"'.format(
                    " ".join([shell.quote(arg) for arg in filegroup.install_args]),
                    path,
                    target_path,
                ))
                content.append("")
                content.append(".PHONY: {}".format(diff_name))
                content.append("diff: {}".format(diff_name))
                content.append("{}:".format(diff_name))
                content.append('\t diff {} "{}" "{}"'.format(
                    " ".join([shell.quote(arg) for arg in filegroup.diff_args]),
                    path,
                    target_path,
                ))
    content.append("")

    ctx.actions.write(
        output = makefile,
        content = "\n".join(content),
    )

    package_filegroup_info = PackageFilegroupInfo(
        pkg_files = pkg_files,
        pkg_dirs = pkg_dirs,
        pkg_symlinks = pkg_symlinks,
    )

    return [
        DefaultInfo(
            files = depset([makefile]),
        ),
        package_filegroup_info,
        AlMakeInstallInfo(
            filegroups = filegroups,
        ),
    ]

al_make_install = rule(
    doc = "Create a make install archive filegroup",
    implementation = _impl,
    provides = [AlMakeInstallInfo, PackageFilegroupInfo],
    attrs = {
        "srcs": attr.label_list(
            providers = [PackageFilegroupInfo],
            doc = "Sources to install",
        ),
        "install_args": attr.string_list(
            default = ["--compare", "--backup", "numbered"],
            doc = "Install args",
        ),
        "diff_args": attr.string_list(
            default = [],
            doc = "Diff args",
        ),
        "deps": attr.label_list(
            providers = [AlMakeInstallInfo],
            doc = "Deps",
        ),
        "install_dir": attr.string(
            default = "${HOME}",
            doc = "Install directory",
        ),
    },
)
