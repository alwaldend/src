load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("//bzl/rules/git:al_git_changelog.bzl", "al_git_changelog")
load("//tools/bzl:al_bzl_target_doc.bzl", "al_bzl_target_doc")
load("//tools/drawio:al_drawio_run_binary.bzl", "al_drawio_run_binary")

_DIAGRAMS = {
    "arch_idea_anki_chinese_flashcards_enricher": {},
    "arch_interview_yandex_sre": {},
}

pkg_filegroup(
    name = "docs",
    srcs = ["{}.doc".format(diagram) for diagram, _ in _DIAGRAMS.items()],
    prefix = package_name(),
    visibility = ["//:__subpackages__"],
)

[
    [
        pkg_files(
            name = "{}.doc".format(diagram),
            srcs = [
                "{}.doc_md".format(diagram),
                "{}.doc_svg".format(diagram),
            ],
            renames = {
                "{}.doc.md".format(diagram): "{}/_index.md".format(diagram),
                "{}.doc.svg".format(diagram): "{}/diagram.svg".format(diagram),
            },
        ),
        write_file(
            name = "{}.doc_md".format(diagram),
            out = "{}.doc.md".format(diagram),
            content = [
                "---",
                "title: {}".format(diagram),
                "tags: [generated]",
                "---",
                "{{< svg_file file=diagram.svg >}}",
            ],
        ),
        al_drawio_run_binary(
            name = "{}.doc_svg".format(diagram),
            srcs = ["{}.drawio".format(diagram)],
            out = "{}.doc.svg".format(diagram),
            arguments = [
                "$(execpath :{}.drawio)".format(diagram),
                "--crop",
                "--export",
                "--embed-diagram",
                "--format",
                "svg",
                "--output",
                "$(execpath :{}.doc.svg)".format(diagram),
            ],
        ),
    ]
    for diagram, _ in _DIAGRAMS.items()
]

al_git_changelog(
    name = "changelog",
    visibility = ["//:__subpackages__"],
)

al_bzl_target_doc(
    name = "bazel_target_doc",
    visibility = ["//:__subpackages__"],
)
