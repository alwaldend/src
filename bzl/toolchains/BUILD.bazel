load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")
load("//bzl/macros:al_bzl_library_map.bzl", "al_bzl_library_map")
load("//bzl/macros:al_bzl_target_doc.bzl", "al_bzl_target_doc")
load("//bzl/macros:al_gen_changelog.bzl", "al_gen_changelog")
load("//bzl/macros:al_readme.bzl", "al_readme")
load(":al_qt_local_toolchain.bzl", "al_qt_local_toolchain")
load(":al_qt_resolved_toolchain.bzl", "al_qt_resolved_toolchain")

_TOOLCHAINS = {
    "qt-local-linux-gcc_64": "//bzl/toolchain-types:qt",
}

al_gen_changelog(
    name = "changelog",
    visibility = ["//visibility:public"],
)

al_readme(
    name = "readme",
    visibility = ["//visibility:public"],
)

al_bzl_library_map(
    name = "toolchains",
    libs = {
        "al_qt_local_toolchain": [
            "@bazel_skylib//lib:paths",
        ],
        "al_qt_resolved_toolchain": [
            "//bzl/rules/resolved_toolchain:al_resolved_toolchain",
        ],
    },
    visibility = ["//visibility:public"],
)

[
    toolchain(
        name = toolchain_name,
        toolchain = ":{}-impl".format(toolchain_name),
        toolchain_type = toolchain_type,
        visibility = ["//visibility:public"],
    )
    for toolchain_name, toolchain_type in _TOOLCHAINS.items()
]

py_console_script_binary(
    name = "aqt",
    pkg = "@com-alwaldend-src-starlark-bazel-qt-requirements//aqtinstall",
)

genrule(
    name = "qt-install",
    outs = ["qt-install.sh"],
    cmd = """
cat - >$(@) <<EOF
    #!/usr/bin/env sh
    set -eux
    mkdir -p /opt/qt
    $(rootpath :aqt) install-qt -O /opt/qt linux desktop 6.9.0
EOF
    """,
    executable = True,
    tools = [":aqt"],
)

compile_pip_requirements(
    name = "requirements",
    srcs = [":requirements.in"],
    requirements_txt = ":requirements.txt",
)

al_qt_local_toolchain(
    name = "qt-local-linux-gcc_64-impl",
    dir = "/opt/qt",
    platform = "gcc_64",
    version = "6.9.0",
)

al_qt_resolved_toolchain(
    name = "qt-resolved",
    visibility = ["//visibility:public"],
)

al_bzl_target_doc(
    name = "bzl-target-doc",
    visibility = ["//visibility:public"],
)
