load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("//tools/ansible:al_ansible_binary.bzl", "al_ansible_binary")
load("//tools/ansible:al_ansible_scripts.bzl", "AL_ANSIBLE_SCRIPTS")
load("//tools/docs:al_docs_files.bzl", "al_docs_files")

al_docs_files(
    name = "docs",
    srcs = glob(["*.md"]),
    prefix = package_name(),
    visibility = ["//infra:__pkg__"],
)

_ARGS = {
    "ansible_playbook": [
        "--inventory",
        "inventory.yaml",
    ],
}

[
    al_ansible_binary(
        name = script,
        arguments = _ARGS.get(script, []),
        process_name = script,
        symlinks = [
            ":srcs",
            "//projects/ansible_collection:collections",
        ],
    )
    for script in AL_ANSIBLE_SCRIPTS
]

pkg_filegroup(
    name = "srcs",
    srcs = [
        ":config",
        ":inventory",
        "//infra/ansible/group_vars",
        "//infra/ansible/host_vars",
    ],
)

pkg_files(
    name = "inventory",
    srcs = ["inventory.yaml"],
)

pkg_files(
    name = "config",
    srcs = ["ansible.cfg"],
)
