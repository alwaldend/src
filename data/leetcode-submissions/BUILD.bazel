load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//bzl/macros:al_gen_changelog.bzl", "al_gen_changelog")
load("//bzl/macros:al_gen_leetcode_submissions.bzl", "al_gen_leetcode_submissions")
load("//bzl/rules/bzl:al_bzl_target_doc.bzl", "al_bzl_target_doc")
load("//bzl/rules/readme:al_readme.bzl", "al_readme")

_LANGS = [
    "cpp",
    "go",
    "java",
    "py",
    "sh",
    "sql",
]

al_readme(
    name = "readme",
    visibility = ["//visibility:public"],
)

al_gen_changelog(
    name = "changelog",
    visibility = ["//visibility:public"],
)

[
    [
        al_gen_leetcode_submissions(
            name = "leetcode-submissions-{}".format(lang),
            srcs = glob(["languages/{}/questions/*/submissions/*/submission.toml".format(lang)]),
            package_dir = "{}/leetcode-submissions".format(lang),
        ),
        write_file(
            name = "leetcode-submissions-{}-index".format(lang),
            out = "leetcode-submissions-{}-index.md".format(lang),
            content = [
                "---",
                "title: Leetcode submissions",
                "---",
            ],
        ),
    ]
    for lang in _LANGS
]

pkg_tar(
    name = "leetcode-submissions",
    srcs = ["leetcode-submissions-{}-index".format(lang) for lang in _LANGS],
    remap_paths = {
        "/leetcode-submissions-{}-index.md".format(lang): "{}/leetcode-submissions/_index.md".format(lang)
        for lang in _LANGS
    },
    visibility = ["//visibility:public"],
    deps = ["leetcode-submissions-{}".format(lang) for lang in _LANGS],
)

al_bzl_target_doc(
    name = "bazel_target_doc",
    visibility = ["//visibility:public"],
)
