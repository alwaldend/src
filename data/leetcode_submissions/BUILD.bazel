load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@rules_pkg//pkg:mappings.bzl", "pkg_filegroup", "pkg_files")
load("//bzl/rules/bzl:al_bzl_target_doc.bzl", "al_bzl_target_doc")
load("//bzl/rules/git:al_git_changelog.bzl", "al_git_changelog")
load("//bzl/rules/leetcode:al_leetcode_submissions.bzl", "al_leetcode_submissions")

_LANGS = [
    "cpp",
    "go",
    "java",
    "py",
    "sh",
    "sql",
]

al_git_changelog(
    name = "changelog",
    visibility = ["//visibility:public"],
)

[
    [
        al_leetcode_submissions(
            name = "leetcode_submissions.{}".format(lang),
            srcs = glob(["languages/{}/questions/*/submissions/*/submission.toml".format(lang)]),
            package_dir = "{}/leetcode_submissions".format(lang),
        ),
        write_file(
            name = "leetcode_submissions.{}_index".format(lang),
            out = "leetcode_submissions.{}_index.md".format(lang),
            content = [
                "---",
                "title: Leetcode submissions",
                "description: Leetcode submissions",
                "---",
            ],
        ),
    ]
    for lang in _LANGS
]

pkg_files(
    name = "leetcode_submissions_docs",
    srcs = ["leetcode_submissions.{}_index".format(lang) for lang in _LANGS],
    renames = {
        "leetcode_submissions.{}_index.md".format(lang): "{}/leetcode_submissions/_index.md".format(lang)
        for lang in _LANGS
    },
)

pkg_filegroup(
    name = "leetcode_submissions",
    srcs = [":leetcode_submissions_docs"] + ["leetcode_submissions.{}".format(lang) for lang in _LANGS],
    visibility = ["//visibility:public"],
)

al_bzl_target_doc(
    name = "bazel_target_doc",
    visibility = ["//visibility:public"],
)
